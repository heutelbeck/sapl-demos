/*
 * This set of demo policies illustrates how JWT can be used in tandem with SAPL.
 *
 * The JWT PIP reads the bearer token securely from subscription secrets and provides
 * decoded claims and validation status via the <jwt.token> environment attribute.
 *
 * The bearer token never enters the SAPL evaluation context. It is only accessible
 * to PIPs through the AttributeAccessContext. Enable automatic injection with:
 *     io.sapl.jwt.inject-token=true
 */

set "JWT Demo Policies"
first or abstain errors propagate

/*
 * The Spring Security libraries for OAuth2 JWT integration map each element of
 * the "scope" array in a JWT token to a Spring Principal authority, prepending
 * "SCOPE_" to each string representing the scope.
 * These can be referenced as any "ROLE_" or other authority provided by Spring
 * Security.
 *
 * This policy does not require the JWT PIP at all. It uses standard Spring
 * Security authorities.
 */
policy "Scopes as Authority in Principal"
permit resource == "books";
	"SCOPE_books.read" in subject..authority;

/*
 * The JWT PIP reads the bearer token from subscription secrets and decodes
 * its claims. This is the recommended way to access JWT data in policies.
 * The token is validated cryptographically against the configured public key
 * server.
 */
policy "Reading scopes from JWT via PIP"
permit resource == "faculty";
	"faculty.read" in <jwt.token>.payload.scope;

/*
 * Another example of reading JWT claims via the PIP. The <jwt.token>
 * attribute returns an object with header, payload, valid, and validity
 * fields. Claims are accessible under payload.
 */
policy "Reading bestiary scope from JWT via PIP"
permit resource == "bestiary";
	"bestiary.read" in <jwt.token>.payload.scope;

/*
 * This policy uses the validity check of the JWT PIP. The token is checked
 * against the configured public key server. The policy will evaluate to
 * permit only while the token signature is valid and the token has not
 * expired. When the token expires, the PIP reactively re-emits and the
 * decision changes to deny.
 */
policy "Policy to test timeout of token"
permit resource == "mysteries";
	<jwt.token>.valid;
